plugins {
    id "com.jfrog.artifactory" version "3.0.1"
}
apply plugin: 'cpp'
apply plugin: 'java'
apply plugin: 'maven-publish'

archivesBaseName = 'jtnoodle'
version = '0.0.1-SNAPSHOT'
group = 'tk.tnoodle'

sourceCompatibility = 1.6

libraries {
    cnoodle {}
    jnitnoodle {}
    pytnoodle {}
}

executables {
    main {}
}

sources {
    main {
        cpp {
            lib libraries.cnoodle
        }
    }
    jnitnoodle {
        cpp {
            source {
                srcDir "$buildDir/jnitnoodle-sources"
                // See pytnoodle.cpp.source comment for why
                // we're doing this.
                srcDir "$projectDir/src/pytnoodle/cpp"
            }
            lib libraries.cnoodle.static
        }
    }
    pytnoodle {
        cpp {
            source {
                srcDir "$buildDir/pytnoodle-sources"
                // Gradle skips the myPytnoodleSharedLibrary when it sees that
                // "$buildDir/pytnoodle-sources" doesn't contain any c code. To hack
                // around this, I've placed an empty c header file (force.h) in
                // $projectDir/src/pytnoodle/cpp to force compilation to occur.
                srcDir "$projectDir/src/pytnoodle/cpp"
            }
            lib libraries.cnoodle.static
        }
    }
}
// Ignore non c/c++ files everywhere
for(source in sources) {
    if(!source.hasProperty('cpp')) {
        continue
    }
    source.cpp.source.includes += "**/*.cpp"
    source.cpp.source.includes += "**/*.c"
    source.cpp.source.includes += "**/*.h"
}

sourceSets {
    jtnoodle {
        java{
            srcDir "$projectDir/src/jtnoodle/java"
            srcDir "$buildDir/jnitnoodle-sources/"
        }
    }
}

def exists(bin) {
    ByteArrayOutputStream stdOut = new ByteArrayOutputStream()
    def r = exec {
        commandLine = [
            "bash", "-c", "which ${bin} 2>&1"
        ]
        standardOutput = stdOut
        ignoreExitValue = true
    }
    if(r.exitValue == 0) {
        // which found the file
        return true
    } else if(r.exitValue == 1) {
        // which failed to find the file
        return false
    } else {
        // Some other error occurred while invoking bash
        throw new GradleScriptException(stdOut.toString(), null)
    }
}

def pythonIncludes = new ByteArrayOutputStream()
exec {
    executable = 'python3-config'
    args = [ '--includes' ]
    standardOutput = pythonIncludes
}

model {
    platforms {
        my {}
        linux_x86_64 {
            architecture "x86_64"
            operatingSystem "linux"
        }
        linux_x86 {
            architecture "x86"
            operatingSystem "linux"
        }
        windows_x86_64 {
            architecture "x86_64"
            operatingSystem "windows"
        }
        windows_x86 {
            architecture "x86"
            operatingSystem "windows"
        }
        // We're only targeting 64-bit binaries on OS X for now.
        osx_x86_64 {
            architecture "x86_64"
            operatingSystem "osx"
        }
    }
    toolChains {
        // We only support building with clang.
        //gcc(Gcc) 
        clang(Clang)

        if(exists("x86_64-w64-mingw32-g++")) {
            mingw(Gcc) {
                target "windows_x86_64"
                // Call to eachPlatform comes from
                // https://github.com/gradle/gradle/commit/f0040d4520a748744c7f7bccf111ba2e1422e34f
                eachPlatform {
                    cCompiler.executable = "x86_64-w64-mingw32-gcc"
                    cppCompiler.executable = "x86_64-w64-mingw32-g++"
                    // http://stackoverflow.com/a/4981870
                    linker.executable = "x86_64-w64-mingw32-g++"
                    assembler.executable = "x86_64-w64-mingw32-as"
                    staticLibArchiver.executable = "x86_64-w64-mingw32-ar"
                }
            }
        }
        if(exists("i686-w64-mingw32-g++")) {
            mingw(Gcc) {
                target "windows_x86"
                eachPlatform {
                    cCompiler.executable = "i686-w64-mingw32-gcc"
                    cppCompiler.executable = "i686-w64-mingw32-g++"
                    // http://stackoverflow.com/a/4981870
                    linker.executable = "i686-w64-mingw32-g++"
                    assembler.executable = "i686-w64-mingw32-as"
                    staticLibArchiver.executable = "i686-w64-mingw32-ar"
                }
            }
        }
        if(exists("x86_64-apple-darwin13-clang++")) {
            osxcross(Clang) {
                target "osx_x86_64"
                eachPlatform {
                    cCompiler.executable = "x86_64-apple-darwin13-clang"
                    cppCompiler.executable = "x86_64-apple-darwin13-clang++"
                    // http://stackoverflow.com/a/4981870
                    linker.executable = "x86_64-apple-darwin13-clang++"
                    assembler.executable = "x86_64-apple-darwin13-as"
                    staticLibArchiver.executable = "x86_64-apple-darwin13-ar"
                }
            }
        }
        all {
            eachPlatform {
                cppCompiler.withArguments { args ->
                    args << "-g" // debugging symbols
                    args << "-Wall"
                    args << "-I" << "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"
                    pythonIncludes.toString().split().each { arg ->
                        args << arg
                    }
                    if(cppCompiler.executable.contains("mingw")) {
                        // Don't bother with fPIC for windows.
                        // The latest version of g++-mingw-w64 for travis ci's
                        // Ubuntu 12.04 is 4.6.3, which doesn't have support
                        // for c++11.
                        args << "-std=c++0x"
                    } else if(cppCompiler.executable.contains("darwin")) {
                        // OS X
                        args << "-fPIC"
                        args << '-I' << "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
                        args << '-I' << "/System/Library/Frameworks/JavaVM.framework/Versions/A/Headers/"
                        args << "-std=c++11"
                    } else {
                        // Linux
                        args << "-fPIC"
                        args << '-I' << "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                        args << "-std=c++11"
                    }
                    // In order to compile for Windows and OS X on Linux, we need
                    // JNI headers. Rather than installing the JDKs for those
                    // respective platforms, it seems to work if we just reuse
                    // the Linux headers.
                    args << '-I' << "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                }
            }
            tasks.withType(CppCompile) { task ->
                task.dependsOn swigj
                if(task.name.toLowerCase().contains("mypytnoodle")) {
                    task.dependsOn swigpy
                }
            }
            tasks.withType(JavaCompile) { task ->
                if(task.name.toLowerCase().contains("jtnoodle")) {
                    task.dependsOn swigj
                }
            }
        }
    }
}

task swigpy {
    def inputFile = file("$projectDir/src/pytnoodle/pytnoodle.i")
    def outputFile = file("$buildDir/pytnoodle-sources/pytnoodle_wrap.cpp")
    inputs.files inputFile
    outputs.file outputFile
    doLast {
        outputFile.parentFile.mkdirs()
        exec {
            executable org.gradle.internal.jvm.Jvm.current().getExecutable('swig')
            args '-o', outputFile
            args '-python'
            args '-builtin'
            args '-c++'

            // TODO - can't figure out how to generate these from
            // source.cnoodle
            args '-Isrc/cnoodle/cpp'
            args '-Isrc/cnoodle/headers'

            args inputFile
        }
    }
}

task swigj {
    def inputFile = file("$projectDir/src/jnitnoodle/jnitnoodle.i")
    def outputFile = file("$buildDir/jnitnoodle-sources/jnitnoodle_wrap.cpp")
    inputs.files inputFile
    outputs.file outputFile
    doLast {
        outputFile.parentFile.mkdirs()
        exec {
            executable org.gradle.internal.jvm.Jvm.current().getExecutable('swig')
            args '-o', outputFile
            args '-java'
            args '-c++'

            // TODO - can't figure out how to generate these from
            // source.cnoodle
            args '-Isrc/cnoodle/cpp'
            args '-Isrc/cnoodle/headers'

            args inputFile
        }
    }
}

def pytnoodleBuildDir = file("$buildDir/pytnoodle/")
task copySwigPythonLibrary(type: Copy) {
    dependsOn 'myPytnoodleSharedLibrary'
    pytnoodleBuildDir.mkdirs()

    from {
        files("$buildDir/pytnoodle-sources/")
    }
    include "*.py"
    into pytnoodleBuildDir
}

task copySwigSharedLibrary(type: Copy) {
    dependsOn 'myPytnoodleSharedLibrary'
    pytnoodleBuildDir.mkdirs()

    // Rename shared library file so swig's python wrapper code can
    // find it. Note that this doesn't change the soname, but that doesn't
    // seem to be an issue.
    rename '(.+)\\.([^.]+)', '_pytnoodle.$2'
    from {
        files("$buildDir/binaries/pytnoodleSharedLibrary/my/")
    }
    include "*"
    into pytnoodleBuildDir
}

task pytnoodle {
    dependsOn copySwigSharedLibrary, copySwigPythonLibrary
}

// Override the test task provided by Java
task test(overwrite: true) {
    dependsOn pytnoodle

    doLast {
        exec {
            executable org.gradle.internal.jvm.Jvm.current().getExecutable('python3')
            args '-m', 'unittest'
            args 'discover'
            args '--start-directory', 'src/test'
            args '--pattern' ,'*Test.py'
            args '-v'
        }
    }
}

// Task to build all jni shared libraries that we are capable of building.
task buildJniAllPlatforms {
    dependsOn binaries.withType(SharedLibraryBinary).matching {
        return it.buildable && it.component.getName() == 'jnitnoodle'
    }
    dependsOn jtnoodleClasses
}

task copyJniLibraries(type: Copy, dependsOn: buildJniAllPlatforms) {
    from {
        libraries.jnitnoodle.binaries.withType(SharedLibraryBinary).matching {
            return it.targetPlatform.name != "my";
        }.collect {
            def containingFolder = it.sharedLibraryFile.getParentFile()
            fileTree(dir: containingFolder.getParentFile(),
                     include: containingFolder.getName() + "/**")
        }
    }
    into new File(sourceSets.jtnoodle.output.classesDir, "/native/")
}

task run(type: JavaExec, dependsOn: copyJniLibraries) {
    main = "Main"
    classpath = sourceSets.jtnoodle.runtimeClasspath
}

jar {
    from sourceSets.jtnoodle.output
    manifest {
        attributes 'Main-Class': 'Main'
    }
}
jar.dependsOn copyJniLibraries

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.jtnoodle.java.srcDirs
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Produce jtnoodle.jar instead of libtnoodle.jar
            artifactId "jtnoodle"
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            pom.withXml {
                // Trick from
                // http://mike-neck.github.io/blog/2013/06/21/how-to-publish-artifacts-with-gradle-maven-publish-plugin-version-1-dot-6/
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'jtnoodle'
                    description 'Scrambling software for WCA competitions'
                    url 'https://github.com/jfly/libtnoodle'
                    scm {
                        url 'https://github.com/jfly/libtnoodle'
                        connection 'https://github.com/jfly/libtnoodle.git'
                        developerConnection 'git@github.com:jfly/libtnoodle.git'
                    }
                    licenses {
                        license {
                            name 'GNU General Public License, Version 3'
                            url 'http://www.gnu.org/licenses/gpl-3.0-standalone.html'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'jfly'
                            name 'Jeremy Fleischman'
                            email 'jeremyfleischman@gmail.com'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

def ENV = System.getenv()
def artifactory_user = null
if(hasProperty('artifactory_user')) {
    artifactory_user = property('artifactory_user')
} else {
    artifactory_user = ENV['artifactory_user']
}
def artifactory_password = null
if(hasProperty('artifactory_password')) {
    artifactory_password = property('artifactory_password')
} else {
    artifactory_password = ENV['artifactory_password']
}

artifactory {
    contextUrl = "http://oss.jfrog.org/artifactory"
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
        defaults {
            //This closure defines defaults for all 'artifactoryPublish' tasks
            // of all projects the plugin is applied to
            publications ('mavenJava')
        }
    }
    resolve {
        repository {
            repoKey = 'libs-release'
            username = "${artifactory_user}"
            password = "${artifactory_password}"
            maven = true
        }
    }
}

task wrapper(type: Wrapper) {
    // We need a version of gradle with
    // https://github.com/gradle/gradle/commit/0cd1cff47d044f038db61287c4d9e0fd93691951
    gradleVersion = '2.2-20140911220012+0000'
}
